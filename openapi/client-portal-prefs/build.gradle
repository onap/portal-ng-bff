plugins {
  id 'java'
  id 'idea'
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id 'org.openapi.generator'
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation "org.openapitools:openapi-generator:$openapiVersion"
}

openApiGenerate {
    generatorName = "java"
    library = "webclient"
    inputSpec = "${project.projectDir}/src/portal_prefs_openapi.yaml"
    outputDir = "${project.buildDir}/openapi"
    configOptions = [
      openApiNullable     : "false",
      dateLibrary         : "java8",
      serializationLibrary: "jackson"
    ]
    typeMappings = [
            "File": "org.springframework.core.io.buffer.DataBuffer"
    ]
    generateApiTests = false
    generateApiDocumentation = false
    generateModelTests = false
    generateModelDocumentation = false
    invokerPackage = "org.onap.portalng.bff.openapi.client_portal_prefs"
    apiPackage = "org.onap.portalng.bff.openapi.client_portal_prefs.api"
    modelPackage = "org.onap.portalng.bff.openapi.client_portal_prefs.model"
    modelNameSuffix = "PortalPrefsDto"
}

compileJava {
  dependsOn tasks.openApiGenerate
}

sourceSets {
  main {
    java {
      srcDirs += file("$buildDir/openapi/src/main/java")
    }
  }
}

idea {
  module {
    generatedSourceDirs += file("$buildDir/openapi/src/main/java")
  }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}