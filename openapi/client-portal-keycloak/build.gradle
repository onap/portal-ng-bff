plugins {
  id 'org.openapi.generator'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.openapitools:openapi-generator:$openapiVersion"
}

compileJava {
    dependsOn = ['openApiGenerate_keycloak']
}

sourceSets {
    main {
        java {
            srcDirs += file("$buildDir/openapi/src/main/java")
        }
    }
}

idea {
    module {
        generatedSourceDirs += file("$buildDir/openapi/src/main/java")
    }
}

tasks.register('openApiGenerate_keycloak', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "java"
    library = "webclient"
    inputSpec = "${project.projectDir}/src/portal_keycloak_openapi.yaml"
    outputDir = "${buildDir}/openapi"
    configOptions = [
      openApiNullable     : "false",
      dateLibrary         : "java8",
      serializationLibrary: "jackson"
    ]
    typeMappings = [
            "File": "org.springframework.core.io.buffer.DataBuffer"
    ]
    generateApiTests = false
    generateApiDocumentation = false
    generateModelTests = false
    generateModelDocumentation = false
    invokerPackage = "org.onap.portalng.bff.openapi.client_portal_keycloak"
    apiPackage = "org.onap.portalng.bff.openapi.client_portal_keycloak.api"
    modelPackage = "org.onap.portalng.bff.openapi.client_portal_keycloak.model"
    modelNameSuffix = "KeycloakDto"
}